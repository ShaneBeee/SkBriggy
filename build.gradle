import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'com.shanebeestudios'

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

// SkBriggy version
version = '1.4.1'

repositories {
    mavenCentral()

    // Paper
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

    // Skript
    maven { url 'https://repo.skriptlang.org/releases' }

    // JitPack
    maven { url 'https://jitpack.io' }

    // Command API Snapshots
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    // Skript
    compileOnly(group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0') {
        transitive = false
    }

    // Command Api
    implementation("dev.jorel:commandapi-bukkit-shade:9.5.3")

    // SkBee
    compileOnly("com.github.ShaneBeee:SkBee:3.5.8")
}

build {
    dependsOn(shadowJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: ["version": version]
}

shadowJar {
    archiveClassifier = null
    relocate("dev.jorel.commandapi", "com.shanebeestudios.briggy.api.commandapi")
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from shadowJar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/1-21-1/plugins'
}
